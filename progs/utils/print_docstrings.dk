
# This utility prints out the docstrings of the entire Deck systems.
# This is not good code.

# Note: Base indentation is determined by the first line with any
# leading spaces.  Be careful not to break that.


use IO
use Perl::Regex



const splitLine = [splitsub '\n']
const hasLeadingSpace = [matchsub '^\s+']

proc leadingSpace {doc} {
    "Find the base indentation level of 'doc'.  This is currently done
     by taking the indentation level of the first line found to be indented 
     past zero."
    var lines = [splitLine doc]

    for ln in lines {
        if [hasLeadingSpace ln] {
            [replacesub '^(\s+)' {|l| return l.size}] ln
        }
    }

    return 0
}


const splitPara = [splitsub '\n\n+']
const trimTrailingSpc = [replacesub '\s*\z' {=""}]

proc trimSpc {doc} {
    "Given a docstring, correct the indentation to compensate for
     the padding normally put in to make it look good in source code."
    var paragraphs = [splitPara doc]

    # Figure out how much space to trim from the front of a line to
    # keep verbatim blocks intact.
    var indent = [leadingSpace doc]
    const trimLeadingSpc = [replacesub "^\\s{$indent}" {=""}]

    paragraphs = [map {|p|
        p = [trimLeadingSpc p]
        next [trimTrailingSpc p]
    } paragraphs]

    return [join "\n\n" paragraphs]
}



proc isProcOrMProc {doc} {
    "Test if C<doc> is a proc or mproc."

    (doc@0 == :proc && ([slice (doc@1) 0 3] != '__::')) && [return true]
    (doc@0 == :mproc) && [return true]
    return false
}

proc printFunctions {mproc pkg} {
    var filter = :proc
    var heading = "Procedures"
    var procType = "proc"

    if (mproc) {
        filter = :mproc
        heading = "MProcs"
        procType = "mproc"
    }

    var keys = [_::docstring_keys filter pkg]
    (keys.size > 0) || [return]

    puts "=head3 $heading\n"
    puts "=over\n\n"

    for key in keys {
        var doc = [_::docstring_get key]

        var ftag = (doc@0)
        var fname = (doc@1)
        var fbuiltin = (doc@2)
        var fnargs = (doc@3)
        var fdoc = [trimSpc (doc@4)]

        var biText = ""
        fbuiltin && (biText = " B<(Built-in)>")

        if (ftag != :mproc) {
            fnargs = "{$fnargs}"
        }

        puts "=item B<$procType $fname $fnargs>$biText\n"

        puts fdoc "\n"
    }

    puts "\n\n=back\n"
}


const podClean = \
    [replacesub '([<>])' {|elem| ="E<"+[if (elem == '<'){='lt'}{='gt'}]+">"} ]

proc printMacros {pkg} {
    var keys = [_::docstring_keys :macro pkg]
    (keys.size > 0) || [return]

    puts "=head3 Macros\n"
    puts "=over\n\n"

    for key in keys {
        var doc = [_::docstring_get key]

        var ftag = (doc@0)
        var fname = [podClean (doc@1)]
        var fbuiltin = (doc@2)
        var fnargs = (doc@3)
        var fdoc = [trimSpc (doc@4)]

        var biText = ""
        fbuiltin && (biText = " B<(Built-in)>")

        puts "=item B<macro $fname {$fnargs}>$biText\n"

        puts fdoc "\n"
    }

    puts "\n\n=back\n"
}




proc printMethodDoc {doc} {
    var builtin = (doc@2)
    var methodName = (doc@4)
    var args = (doc@5)
    var fdoc = [trimSpc (doc@6)]
    
    var biText = ""
    builtin && (biText = " B<(Built-in)>")

    puts "=item B<method $methodName {$args}>$biText\n"
    puts fdoc "\n"
}



proc uniq {aSeq} {
    var result = [aSeq.maker aSeq.size]

    var index = 0
    var prev = nil
    for elem in aSeq {
        if (elem != prev) {
            result@index = elem
            prev = elem
            index = index + 1
        }
    }

    return [slice result 0 (index - 1)]
}

const attrib_re = '_[gs]et$'
const stripAttrib = [replacesub attrib_re {=""}]
const isAttrib = [matchsub attrib_re]

proc fixAttrName {attr} {
    (attr && [isAttrib attr]) || [return ""]
    return [stripAttrib attr]
}

proc printAttribDoc {doc} {
    var builtin = (doc@2)
    var access = [unintern (doc@4)]
    var attribName = (doc@5)
    var docstr = [trimSpc (doc@6)]
    
    var biText = ""
    builtin && (biText = " B<(Built-in)>")

    puts "=item B<$access $attribName>$biText\n"
    puts docstr "\n"
}

proc printAttributes {name} {
    var cl = [lookup [intern name]]

    var attributes = [map {fixAttrName [unintern a]} cl.methods]
    attributes = [filter {= a} attributes]
    attributes = [sort cmp attributes]
    attributes = [uniq attributes]

    if (attributes.size == 0) {
        return
    }

    puts "I<Attributes>\n"
    puts "=over\n"

    for attr in attributes {
        var doc = [_::docstring_get "$name.$attr"]
        doc && [printAttribDoc doc]
    }

    puts "=back\n"
}


proc printMethods {name} {
    var cl = [lookup [intern name]]
    var selectors = [sort cmp cl.selectors]

    if (selectors.size == 0) {
        return
    }

    puts "I<Methods>\n"
    puts "=over\n"

    foreach sel in selectors {
        var doc = [_::docstring_get "$name->$sel"]
        if (doc) {
            printMethodDoc doc
        }
    }

    puts "=back\n"
}


proc printClasses {pkg} {
    var classNames = [_::docstring_keys :class pkg]
    (classNames.size > 0) || [return]

    puts "=head3 Classes\n\n"
    puts "=over\n\n"

    for classNm in classNames {
        var doc = [_::docstring_get classNm]

        var name = doc@1
        var builtin = doc@2
        var docstring = [trimSpc (doc@3)]

        var biText = ""
        builtin && (biText = " B<-- Built-in>")

        var cl = [lookup [intern name]]

        var sc = 'none'

        (cl == Object) || (sc = cl.superclass.name)
        sc = "($sc)"

        puts "=item B<$name $sc> $biText\n"

        puts docstring "\n"
        
        printAttributes name

        printMethods name

    }

    puts "=back\n"
}

proc printPackageHeader {pkg} {

    var pkgBase = [slice pkg 0 (pkg.last - 2)]
    puts "=head2 Package $pkgBase\n"

    var desc = [_::docstring_get pkg]@2
    puts desc
    puts "\n"
}


proc printPackage {pkg} {
    printPackageHeader pkg
    printFunctions true pkg
    printFunctions false pkg
    printMacros pkg
    printClasses pkg
}


# Lang needs to go first
var packages = [_::docstring_keys :package nil]
packages = [filter { value (a != [intern "Lang::"]) } packages]
packages = [concat [list [intern "Lang::"]] packages]

for pkg in packages {
    printPackage pkg
}

